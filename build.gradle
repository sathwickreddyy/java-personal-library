plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

scmVersion {
    tag { prefix = '' }
    repository {
        remote = 'origin'
        customUsername = System.getenv("GITHUB_ACTOR")  // GitHub username
        customPassword = System.getenv("GITHUB_TOKEN")  // GitHub personal access token
    }
}


group = 'com.java'
version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // lombok
    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // SLF4J API (Logging Facade)
    implementation 'org.slf4j:slf4j-api:2.0.9'
    // Logback (Actual Logging Implementation)
    implementation 'ch.qos.logback:logback-classic:1.4.14'

    // redis
    implementation 'redis.clients:jedis:3.7.0'

    implementation 'org.apache.commons:commons-math3:3.6.1'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar() // Include source code in the published artifact
    withJavadocJar() // Include Javadoc in the published artifact
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sathwickreddyy/java-personal-library")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
